PROJECT(cview)
CMAKE_MINIMUM_REQUIRED(VERSION 2.4.5)
MARK_AS_ADVANCED(CMAKE_BACKWARDS_COMPATIBILITY)
include(CheckFunctionExists)
include(CheckIncludeFile)

SET(VERSION_MAJOR 0)
SET(VERSION_MINOR 3)
SET(VERSION_PATCH 0)
SET(VERSION ${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH})
#options
OPTION(CVIEW_TEST_BUILD "Build CView Testing Applications" OFF)
OPTION(GCOBJECTDEBUG "Enable Tracking of Object Allocations" OFF)
#deal with tweakbar in special directory

#detect libraries
IF (APPLE)
	SET(FOUNDATION_LIBRARIES "-framework Foundation")
ELSEIF (UNIX)
	FIND_PROGRAM(GNUSTEP_CONFIG gnustep-config)
	MESSAGE("Found gnustep-config at: ${GNUSTEP_CONFIG}")
	IF(NOT DEFINED GNUSTEP_CONFIG)
		MESSAGE(FATAL_ERROR "Error: gnustep-config not found. exiting")
	ENDIF()
	EXECUTE_PROCESS(COMMAND ${GNUSTEP_CONFIG} --base-libs  OUTPUT_VARIABLE FOUNDATION_LIBRARIES OUTPUT_STRIP_TRAILING_WHITESPACE)
	EXECUTE_PROCESS(COMMAND ${GNUSTEP_CONFIG} --objc-flags OUTPUT_VARIABLE FOUNDATION_FLAGS OUTPUT_STRIP_TRAILING_WHITESPACE)
	#lets control Optimization and debug on our own please
	STRING(REPLACE "-g -O2" "" FOUNDATION_FLAGS ${FOUNDATION_FLAGS})
	MARK_AS_ADVANCED(GNUSTEP_CONFIG)
ELSEIF (MINGW)
	FILE(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/mingw)
	EXECUTE_PROCESS(
			COMMAND ${CMAKE_COMMAND} -G "MSYS Makefiles" ${CMAKE_SOURCE_DIR}/mingw
			WORKING_DIRECTORY ${CMAKE_BINARY_DIR}/mingw
			RESULT_VARIABLE MINGW_CMAKE_RESULT
			)
	EXECUTE_PROCESS(
			COMMAND ${CMAKE_MAKE_PROGRAM} mingw-libs
			WORKING_DIRECTORY ${CMAKE_BINARY_DIR}/mingw
			RESULT_VARIABLE MINGW_MAKE_RESULT
			)
	IF (MINGW_CMAKE_RESULT OR MINGW_MAKE_RESULT)
		MESSAGE(FATAL_ERROR "
							*************************************************
							MinGW library build failed.  please check output.
							*************************************************
							")
	ENDIF()
	MESSAGE("PATHS: ${CMAKE_INCLUDE_PATH} ${CMAKE_LIBRARY_PATH}")
	SET(CMAKE_INCLUDE_PATH ${CMAKE_BINARY_DIR}/mingw/include)	
	SET(CMAKE_LIBRARY_PATH ${CMAKE_BINARY_DIR}/mingw/libs)	
	ADD_SUBDIRECTORY(mingw)	
ENDIF ()

FIND_PACKAGE(Doxygen)
FIND_PACKAGE(GLUT REQUIRED)
FIND_PACKAGE(OpenGL REQUIRED)
FIND_PACKAGE(ImageMagick COMPONENTS MagickWand REQUIRED)
MARK_AS_ADVANCED(ImageMagick_EXECUTABLE_DIR ImageMagick_MagickWand_INCLUDE_DIR ImageMagick_MagickWand_LIBRARY)
FIND_PACKAGE(PkgConfig)
PKG_CHECK_MODULES(FTGL REQUIRED ftgl)
PKG_CHECK_MODULES(OSMESA osmesa)

FIND_LIBRARY(GENDERS genders)
IF(GENDERS)
	MESSAGE("Using Genders Library at: ${GENDERS}")
	SET(HAVE_GENDERS YES)
ELSE()
	MESSAGE("Genders Library Not Found")
	SET(HAVE_GENDERS NO)
ENDIF()

FIND_LIBRARY(ATB AntTweakBar)
IF(ATB)
    MESSAGE("Using AntTweakBar Library at: ${ATB}")
    SET(HAVE_ANTTWEAKBAR YES)
ELSE()
	MESSAGE("AntTweakBar Library Not Found")
    SET(HAVE_ANTTWEAKBAR NO)
ENDIF()

SET(CMAKE_REQUIRED_INCLUDES ${ImageMagick_MagickWand_INCLUDE_DIR})
SET(CMAKE_REQUIRED_LIBRARIES ${ImageMagick_MagickWand_LIBRARY})
CHECK_FUNCTION_EXISTS(MagickExportImagePixels HAVE_MAGICKEXPORTIMAGEPIXELS)

FIND_PATH(GL_H_INCLUDE "gl.h" PATH_SUFFIXES GL)
MARK_AS_ADVANCED(GL_H_INCLUDE)

SET(PKG_DATA_DIR "${CMAKE_INSTALL_PREFIX}/share/${PROJECT_NAME}")

IF(DOXYGEN_FOUND)
	CONFIGURE_FILE(Doxyfile.in ${CMAKE_BINARY_DIR}/Doxyfile)
	SET(DOXYGEN_OUTPUT ${CMAKE_BINARY_DIR}/html/index.html)
	FILE(GLOB DOXYGEN_INPUT */*.h */*.m */*.c docs/*.dox Doxyfile.in)
	ADD_CUSTOM_COMMAND(OUTPUT ${DOXYGEN_OUTPUT} COMMAND ${DOXYGEN_EXECUTABLE} ${CMAKE_BINARY_DIR}/Doxyfile DEPENDS ${DOXYGEN_INPUT})
	ADD_CUSTOM_TARGET(doc DEPENDS ${DOXYGEN_OUTPUT})
ENDIF()

CONFIGURE_FILE(cview.spec.in ${CMAKE_BINARY_DIR}/cview.spec)
CONFIGURE_FILE(config.h.in ${CMAKE_BINARY_DIR}/config.h)

INCLUDE_DIRECTORIES(${CMAKE_BINARY_DIR})
INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/libcview-data)
INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/libcview)

ADD_SUBDIRECTORY(libcview-data)
LINK_DIRECTORIES(${CMAKE_BINARY_DIR}/libcview-data)
ADD_SUBDIRECTORY(libcview)
LINK_DIRECTORIES(${CMAKE_BINARY_DIR}/libcview)
#ADD_SUBDIRECTORY(data)
IF (CVIEW_TEST_BUILD)
	ADD_SUBDIRECTORY(tests)
ENDIF()
ADD_SUBDIRECTORY(src)

#this is for in tree testing
FILE(COPY "${CMAKE_SOURCE_DIR}/data" DESTINATION ${CMAKE_BINARY_DIR})

#this is for install
FILE(GLOB DATA_FILES "${CMAKE_SOURCE_DIR}/data/*.ttf*" "${CMAKE_SOURCE_DIR}/data/*.svg" "${CMAKE_SOURCE_DIR}/data/*.ggr")
INSTALL(FILES ${DATA_FILES} DESTINATION "share/${CMAKE_PROJECT_NAME}" COMPONENT "Data_Files")  # should we be more specific about which files?
FILE(GLOB MAN_FILES "${CMAKE_SOURCE_DIR}/man/*.1")
INSTALL(FILES ${MAN_FILES} DESTINATION "man/man.1" COMPONENT "Man_Pages")

#-------------------------------------------------------
#Packgaing settings for cpack
SET(CPACK_PACKAGE_NAME ${CMAKE_PROJECT_NAME})
SET(CPACK_PACKAGE_VENDOR "www.emsl.pnl.gov")
SET(CPACK_PACKAGE_DESCRIPTION "CView - Graphics engine for displaying information in a 3 dimensional interactive environment. Somewhat targeted at time series performance data, and cluster displays.")
SET(CPACK_PACKAGE_VERSION "${VERSION}")
SET(CPACK_PACKAGE_VERSION_MAJOR "${VERSION_MAJOR}")
SET(CPACK_PACKAGE_VERSION_MINOR "${VERSION_MINOR}")
SET(CPACK_PACKAGE_VERSION_PATCH "${VERSION_PATCH}")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/docs/copying.txt")
INCLUDE(CPack)
